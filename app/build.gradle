plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def releaseKeystoreProps = new Properties()
def releaseKeystorePropsFile = rootProject.file("keystore/release.properties")
if (releaseKeystorePropsFile.exists()) {
    releaseKeystorePropsFile.withInputStream { stream ->
        releaseKeystoreProps.load(stream)
    }
}

android {
    namespace 'me.osku.freeshiamy'
    compileSdk 34

    defaultConfig {
        applicationId "me.osku.freeshiamy"
        minSdk 23
        targetSdk 34
        versionCode 4
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def storeFilePath = releaseKeystoreProps.getProperty("storeFile", "keystore/freeshiamy-release.jks")
            storeFile rootProject.file(storeFilePath)
            storePassword releaseKeystoreProps.getProperty("storePassword", "freeshiamy")
            keyAlias releaseKeystoreProps.getProperty("keyAlias", "freeshiamy")
            keyPassword releaseKeystoreProps.getProperty("keyPassword", "freeshiamy")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
